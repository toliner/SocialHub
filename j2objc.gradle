buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'idea'
    id 'maven'
    id 'net.socialhub.j2objccontrib.j2objcgradle' version '0.7.1'
}

apply plugin: 'net.socialhub.j2objccontrib.j2objcgradle'

group 'SocialHub'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }
}

dependencies {

    // Translation Exclusion Library
    implementation(Dependencies.JSR_305)

    // Common Library
    implementation(Dependencies.COMMONS_LANG)
    implementation(Dependencies.COMMONS_CODEC)
    implementation(Dependencies.GSON)
    implementation(Dependencies.SCRIBE)

    // J2ObjC Common Library
    implementation(Dependencies.JLOGGER)
    implementation(Dependencies.JHTTP_CLIENT)

    // J2ObjC Network Library
    api(Dependencies.TWITTER4J_CORE)
    api(Dependencies.TWITTER4J_STREAM)
    api(Dependencies.TWITTER_TEXT)
    api(Dependencies.FACEBOOK4J)
    api(Dependencies.JSLACK_CLIENT)
    api(Dependencies.JSLACK_MODEL)
    api(Dependencies.MSINSTANCEJ)
    api(Dependencies.MASTODON4J)
    api(Dependencies.JUMBLR)

    // Test
    testImplementation(Dependencies.JUNIT)
}


j2objcConfig {
    autoConfigureDeps true
    skipJ2objcVerification true

    // Only support 64bits
    // TODO: 'ios_arm64e' should be support
    supportedArchs = ['ios_arm64', 'ios_x86_64']

    translateArgs '--build-closure'
    translateArgs '--swift-friendly'
    translateArgs '--prefixes', 'prefixes.properties'
    extraObjcCompilerArgs '-fembed-bitcode'
    extraLinkerArgs "-liconv"

    finalConfigure()
}

j2objcTranslate.doLast {
    exec {
        executable "sh"
        args './tool/buildheader.sh'
    }
}

j2objcPodspec.doLast {
    [getPodspecDebug(), getPodspecRelease()].asList().each {

        String text = it.text
                .replace(
                        "Pod::Spec.new do |spec|",
                        '''Pod::Spec.new do |spec|
  spec.authors = 'Akihiro Urushihara'
  spec.license = 'MIT'
  spec.homepage = 'https://twitter.com/U_Akihir0'
  spec.source = { :git => 'git@bitbucket.org:your/repo.git' }''')
                .replace(
                        "icucore",
                        "icucore', 'iconv")

        it.write(text)
    }
}