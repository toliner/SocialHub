buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'idea'
    id 'maven'
    id 'net.socialhub.j2objccontrib.j2objcgradle' version '0.7.1'
}

apply plugin: 'net.socialhub.j2objccontrib.j2objcgradle'

group 'SocialHub'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }
}

dependencies {

    // Translation Exclusion Library
    compile(Dependencies.JSR_305)

    // Common Library
    compile(Dependencies.COMMONS_LANG)
    compile(Dependencies.COMMONS_CODEC)
    compile(Dependencies.GSON)
    compile(Dependencies.SCRIBE)

    // J2ObjC Common Library
    compile(Dependencies.JLOGGER)
    compile(Dependencies.JHTTP_CLIENT)

    // J2ObjC Network Library
    compile(Dependencies.TWITTER4J_CORE)
    compile(Dependencies.TWITTER4J_STREAM)
    compile(Dependencies.TWITTER_TEXT)
    compile(Dependencies.FACEBOOK4J)
    compile(Dependencies.JSLACK_CLIENT)
    compile(Dependencies.JSLACK_MODEL)
    compile(Dependencies.MSINSTANCEJ)
    compile(Dependencies.MASTODON4J)
    compile(Dependencies.JUMBLR)

    // Test
    testImplementation(Dependencies.JUNIT)
}


j2objcConfig {
    autoConfigureDeps true
    skipJ2objcVerification true

    // Only support 64bits
    // TODO: 'ios_arm64e' should be support
    supportedArchs = ['ios_arm64', 'ios_x86_64']

    translateArgs '--build-closure'
    translateArgs '--swift-friendly'
    translateArgs '--prefixes', 'prefixes.properties'
    extraObjcCompilerArgs '-fembed-bitcode'
    extraLinkerArgs "-liconv"

    finalConfigure()
}

j2objcTranslate.doLast {
    exec {
        executable "sh"
        args './tool/buildheader.sh'
    }
}

j2objcPodspec.doLast {
    [getPodspecDebug(), getPodspecRelease()].asList().each {

        String text = it.text
                .replace(
                        "Pod::Spec.new do |spec|",
                        '''Pod::Spec.new do |spec|
  spec.authors = 'Akihiro Urushihara'
  spec.license = 'MIT'
  spec.homepage = 'https://twitter.com/U_Akihir0'
  spec.source = { :git => 'git@bitbucket.org:your/repo.git' }''')
                .replace(
                        "icucore",
                        "icucore', 'iconv")

        it.write(text)
    }
}